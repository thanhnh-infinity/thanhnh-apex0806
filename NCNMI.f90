      SUBROUTINE NCNMI(Z5,CS)
!     APEX0806
!     THIS SUBPROGRAM SIMULATES MINERALIZATION AND IMMOBILIZATION OF N
!     AND C USING EQUATIONS TAKEN FROM CENTURY.
      USE PARM
      DATA RHS/.000548/,RHP/.0000123/
      !AD1=WNMN(ISL,ISA)+WNMA(ISL,ISA)+WHSN(ISL,ISA)+WHPN(ISL,ISA)+WNOU&
      !(ISL,ISA)+WLSN(ISL,ISA)+WLMN(ISL,ISA)+WBMN(ISL,ISA)
      LD1=LID(1,ISA)
      XU=WLM(ISL,ISA)+WLS(ISL,ISA)
      XZ=WNMN(ISL,ISA)+WNMA(ISL,ISA)
      XY=WLSN(ISL,ISA)+WLMN(ISL,ISA)
      ZZ=WLSC(ISL,ISA)+WLMC(ISL,ISA)
      XHN=WHSN(ISL,ISA)+WHPN(ISL,ISA)
      RLR=MIN(.8,WLSL(ISL,ISA)/(WLS(ISL,ISA)+1.E-5))
      IF(IOX==0)THEN
          OX=1.-PRMT(53)*Z5/(Z5+EXP(SCRP(20,1)-SCRP(20,2)*Z5))
      ELSE
          Y1=.1*WOC(ISL,ISA)/WT(ISL,ISA)
          Y2=2.11+.0375*CLA(ISL,ISA)
          Y3=100.*Y1/Y2
          OX=Y3/(Y3+EXP(SCRP(25,1)-SCRP(25,2)*Y3))            
      END IF
      X2=PRMT(52)*(BD(ISL,ISA)-BDP(ISL,ISA))
      IF(X2>3.)THEN
          X1=20.
      ELSE
          X1=EXP(X2)
      END IF
      SMS(4,ISL,ISA)=SMS(4,ISL,ISA)+X1
      CS=MIN(10.,SQRT(CDG*SUT)*PRMT(70)*OX*X1)
      SMS(11,ISL,ISA)=SMS(11,ISL,ISA)+1.
      SMS(1,ISL,ISA)=SMS(1,ISL,ISA)+SUT
      APCO2=.55
      ASCO2=.60
      IF(ISL==LD1)THEN
          CS=CS*PRMT(69)*(1.+RSDM(LD1,ISA))
          ABCO2=.55
          A1CO2=.55
          RBM=.0164
          RLM=.0405
          RLS=.0107
          HPNC=.1
          XBM=1.
    !     COMPUTE N/C RATIOS
          X1=.1*(WLMN(LD1,ISA)+WLSN(LD1,ISA))/(RSD(LD1,ISA)+1.E-5)
          IF(X1>2.)THEN
              BMNC=.1
          ELSE
              IF(X1>.01)THEN
                  BMNC=1./(20.05-5.0251*X1)
              ELSE
                  BMNC=.05
              END IF
          END IF
          HSNC=BMNC/(5.*BMNC+1.)
          GO TO 2
      END IF
      ABCO2=.17+.0068*SAN(ISL,ISA)
      A1CO2=.55
      RBM=.02
      RLM=.0507
      RLS=.0132
      XBM=.25+.0075*SAN(ISL,ISA)
      X1=1000.*(WNMA(ISL,ISA)+WNMN(ISL,ISA))/WT(ISL,ISA)
      IF(X1>7.15)THEN
          BMNC=.33
          HSNC=.083
          HPNC=.143
      ELSE
          BMNC=1./(15.-1.678*X1)
          HSNC=1./(20.-1.119*X1)
          HPNC=1./(10.-.42*X1)
      END IF          
    2 ABP=.003+.00032*CLA(ISL,ISA)
      SMS(3,ISL,ISA)=SMS(3,ISL,ISA)+CS
      ASP=MAX(.001,.05-.00009*CLA(ISL,ISA))
!     POTENTIAL TRANSFORMATIONS STRUCTURAL LITTER
      X1=RLS*CS*EXP(-3.*RLR)
      TLSCP=X1*WLSC(ISL,ISA)
      TLSLCP=TLSCP*RLR
      TLSLNCP=TLSCP*(1.-RLR)
      TLSNP=X1*WLSN(ISL,ISA)
!     POTENTIAL TRANSFORMATIONS METABOLIC LITTER
      X1=RLM*CS
      TLMCP=WLMC(ISL,ISA)*X1
      TLMNP=WLMN(ISL,ISA)*X1
!     POTENTIAL TRANSFORMATIONS MICROBIAL BIOMASS
      X1=RBM*CS*XBM
      TBMCP=WBMC(ISL,ISA)*X1
      TBMNP=WBMN(ISL,ISA)*X1
!     POTENTIAL TRANSFORMATIONS SLOW HUMUS
      X1=RHS*CS
      THSCP=WHSC(ISL,ISA)*X1
      THSNP=WHSN(ISL,ISA)*X1
!     POTENTIAL TRANSFORMATIONS PASSIVE HUMUS
      X1=CS*RHP
      THPCP=WHPC(ISL,ISA)*X1
      THPNP=WHPN(ISL,ISA)*X1
!     ESTIMATE N DEMAND
      A1=1.-A1CO2
      ASX=1.-ASCO2-ASP
      APX=1.-APCO2
      PN1=TLSLNCP*A1*BMNC
      PN2=.7*TLSLCP*HSNC
      PN3=TLMCP*A1*BMNC
      PN5=TBMCP*ABP*HPNC
      PN6=TBMCP*(1.-ABP-ABCO2)*HSNC
      PN7=THSCP*ASX*BMNC
      PN8=THSCP*ASP*HPNC
      PN9=THPCP*APX*BMNC
!     COMPARE SUPPLY AND DEMAND FOR N
      SUM=0.
      CPN1=0.
      CPN2=0.
      CPN3=0.
      CPN4=0.
      CPN5=0.
      X1=PN1+PN2
      IF(TLSNP<X1)THEN
          CPN1=X1-TLSNP
      ELSE
          SUM=SUM+TLSNP-X1
      END IF    
      IF(TLMNP<PN3)THEN
          CPN2=PN3-TLMNP
      ELSE
          SUM=SUM+TLMNP-PN3
      END IF    
      X1=PN5+PN6
      IF(TBMNP<X1)THEN
          CPN3=X1-TBMNP
      ELSE
          SUM=SUM+TBMNP-X1
      END IF
      X1=PN7+PN8
      IF(THSNP<X1)THEN
          CPN4=X1-THSNP
      ELSE
          SUM=SUM+THSNP-X1
      END IF
      IF(THPNP<PN9)THEN
          CPN5=PN9-THPNP
      ELSE
          SUM=SUM+THPNP-PN9
      END IF
      WMIN=MAX(1.E-5,WNMN(ISL,ISA)+SUM)
      DMDN=CPN1+CPN2+CPN3+CPN4+CPN5
      X3=1.
!     REDUCE DEMAND IF SUPPLY LIMITS
      IF(WMIN<DMDN)X3=WMIN/DMDN
      SMS(5,ISL,ISA)=SMS(5,ISL,ISA)+X3
!     ACTUAL TRANSFORMATIONS
      TLSCA=TLSCP*X3
      TLSLCA=TLSLCP*X3
      TLSLNCA=TLSLNCP*X3
      TLSNA=TLSNP*X3
      TLMCA=TLMCP*X3
      TLMNA=TLMNP*X3
      TBMCA=TBMCP*X3
      TBMNA=TBMNP*X3
      THSCA=THSCP*X3
      THSNA=THSNP*X3
      THPCA=THPCP*X3
      THPNA=THPNP*X3
      SGMN=SGMN+SUM
      RNMN(ISL)=SUM-DMDN
!     UPDATE
      IF(RNMN(ISL)>0.)THEN
          WNMA(ISL,ISA)=WNMA(ISL,ISA)+RNMN(ISL)
      ELSE
	      X1=WNMN(ISL,ISA)+RNMN(ISL)
	      IF(X1>0.)THEN
	          WNMN(ISL,ISA)=X1
	      ELSE
	          RNMN(ISL)=-WNMN(ISL,ISA)
	          WNMN(ISL,ISA)=1.E-10
	      END IF
      END IF
      DF1=TLSNA
      DF2=TLMNA
      SNMN=SNMN+RNMN(ISL)
	  SMS(9,ISL,ISA)=SMS(9,ISL,ISA)+RNMN(ISL)
      WLSC(ISL,ISA)=MAX(1.E-10,WLSC(ISL,ISA)-TLSCA)
      WLSLC(ISL,ISA)=MAX(1.E-10,WLSLC(ISL,ISA)-TLSLCA)
      WLSLNC(ISL,ISA)=MAX(1.E-10,WLSC(ISL,ISA)-WLSLC(ISL,ISA))
      WLMC(ISL,ISA)=MAX(1.E-10,WLMC(ISL,ISA)-TLMCA)
      WLM(ISL,ISA)=MAX(1.E-10,WLM(ISL,ISA)-TLMCA/.42)
      WLSL(ISL,ISA)=MAX(1.E-10,WLSL(ISL,ISA)-TLSLCA/.42)
      WLS(ISL,ISA)=MAX(1.E-10,WLS(ISL,ISA)-TLSCA/.42)
      X3=APX*THPCA+ASX*THSCA+A1*(TLMCA+TLSLNCA)
      WBMC(ISL,ISA)=WBMC(ISL,ISA)-TBMCA+X3
      WCMU(ISL,ISA)=WCMU(ISL,ISA)-TBMCA+X3
      DF3=TBMNA-BMNC*X3
      X1=.7*TLSLCA+TBMCA*(1.-ABP-ABCO2)
      WHSC(ISL,ISA)=WHSC(ISL,ISA)-THSCA+X1
      DF4=THSNA-HSNC*X1
      X1=THSCA*ASP+TBMCA*ABP
      WHPC(ISL,ISA)=WHPC(ISL,ISA)-THPCA+X1
      DF5=THPNA-HPNC*X1
      DF6=XZ-WNMN(ISL,ISA)-WNMA(ISL,ISA)
      SMS(10,ISL,ISA)=SMS(10,ISL,ISA)-DF6
      ADD=DF1+DF2+DF3+DF4+DF5+DF6
      ADF1=ABS(DF1)
      ADF2=ABS(DF2)
      ADF3=ABS(DF3)
      ADF4=ABS(DF4)
      ADF5=ABS(DF5)
      TOT=ADF1+ADF2+ADF3+ADF4+ADF5
      XX=ADD/(TOT+1.E-10)
      WLSN(ISL,ISA)=MAX(.001,WLSN(ISL,ISA)-DF1+XX*ADF1)
      WLMN(ISL,ISA)=MAX(.001,WLMN(ISL,ISA)-DF2+XX*ADF2)
      WBMN(ISL,ISA)=WBMN(ISL,ISA)-DF3+XX*ADF3
      WHSN(ISL,ISA)=WHSN(ISL,ISA)-DF4+XX*ADF4
      WHPN(ISL,ISA)=WHPN(ISL,ISA)-DF5+XX*ADF5
      RSPC(ISL)=MAX(0.,.3*TLSLCA+A1CO2*(TLSLNCA+TLMCA)+ABCO2*TBMCA+ASCO2*&
      THSCA+APCO2*THPCA)
      SMM(74,MO,ISA)=SMM(74,MO,ISA)+RSPC(ISL)
      SMS(8,ISL,ISA)=SMS(8,ISL,ISA)+RSPC(ISL)
      TRSP=TRSP+RSPC(ISL)      
      VAR(74,ISA)=VAR(74,ISA)+RSPC(ISL)
      X1=WLS(ISL,ISA)+WLM(ISL,ISA)
      RSD(ISL,ISA)=.001*X1
	  VAR(103,ISA)=.001*(XU-X1)
	  IF(RSDM(ISL,ISA)<VAR(103,ISA))VAR(103,ISA)=RSDM(ISL,ISA)
      SMM(103,MO,ISA)=SMM(103,MO,ISA)+VAR(103,ISA)
      X1=MIN(CS,.1)
      XX=RSDM(ISL,ISA)*X1
      RSDM(ISL,ISA)=RSDM(ISL,ISA)-XX
      XX=WNOU(ISL,ISA)*X1
      WNOU(ISL,ISA)=WNOU(ISL,ISA)-XX
      WNMN(ISL,ISA)=WNMN(ISL,ISA)+XX
      IF(IDFH(ISA)>0)THEN
          SMMU(ISA)=SMMU(ISA)+VAR(103,ISA)
!         SLAP=SLAP+VAR(103,ISA)
      END IF
      WCOU(ISL,ISA)=MAX(0.,WCOU(ISL,ISA)+WLSC(ISL,ISA)+WLMC(ISL,ISA)-ZZ)
      XX=XY-WLSN(ISL,ISA)-WLMN(ISL,ISA)
      DHN(ISL,ISA)=XHN-WHSN(ISL,ISA)-WHPN(ISL,ISA)
      !AD2=WNMN(ISL,ISA)+WNMA(ISL,ISA)+WHSN(ISL,ISA)+WHPN(ISL,ISA)+WNOU&
      !(ISL,ISA)+WLSN(ISL,ISA)+WLMN(ISL,ISA)+WBMN(ISL,ISA)
      !DF=AD1-AD2
      !IF(ABS(DF)>.001)WRITE(KW(1),'(A,4I4,20E16.6)')'#####',IY,MO,KDA,ISL,AD1,AD2,DF
      RETURN
      END